load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push")
load("@rules_distroless//apt:defs.bzl", "dpkg_status")


PACKAGES = [
    "@ubuntu_jammy//curl",
    "@ubuntu_jammy//git",
    "@ubuntu_jammy//xz-utils",
]

# Creates /var/lib/dpkg/status with installed package information.
dpkg_status(
    name = "dpkg_status",
    controls = select({
        "@platforms//cpu:x86_64": [
            "%s/amd64:control" % package
            for package in PACKAGES
        ],
    }),
)

sh_binary(
    name = "bazelisk",
    srcs = ["@bazelisk//file:file"],
)

pkg_tar(
    name = "bazelisk_tar",
    srcs = [":bazelisk"],
    strip_prefix = ".",
    package_dir = "/usr/local/bin",
)

oci_image(
    name = "image",
    base = "@actions_runner",
    tars = [
        ":dpkg_status",
        ":bazelisk_tar",
    ] + select({
        "@platforms//cpu:x86_64": [
            "%s/amd64" % package
            for package in PACKAGES
        ],
    }),
    cmd = ["/usr/bin/bash"],
)

oci_push(
    name = "push_image",
    image = ":image",
    remote_tags = ["latest"],
    repository = "ghcr.io/rszamszur/actions-runner",
)
